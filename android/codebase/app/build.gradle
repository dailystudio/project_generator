apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.dailystudio.codebase"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode Integer.parseInt(appVersionCode)
        versionName appVersion
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }


    signingConfigs {

        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }

        debug {
            storeFile file(DEBUG_STORE_FILE)
            storePassword DEBUG_STORE_PASSWORD
            keyAlias DEBUG_KEY_ALIAS
            keyPassword DEBUG_KEY_PASSWORD
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release

            buildConfigField "boolean","USE_STETHO","false"

            resValue "string", "app_name", "@string/app_name_release"
        }

        releaseUnsigned {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "boolean","USE_STETHO","false"

            resValue "string", "app_name", "@string/app_name_release"

            matchingFallbacks = ["release"]
        }

        debug {
            applicationIdSuffix '.dev'

            debuggable true
            signingConfig signingConfigs.debug
            versionNameSuffix " (DEBUG)"

            buildConfigField "boolean","USE_STETHO","true"

            resValue "string", "app_name", "@string/app_name_debug"
        }
    }

    androidExtensions {
        experimental = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation "com.facebook.stetho:stetho:${project.ext.stethoVersion}"
    implementation "org.greenrobot:eventbus:${project.ext.eventBusVersion}"
    implementation "com.nostra13.universalimageloader:universal-image-loader:${project.ext.imageLoaderVersion}"
    implementation "com.squareup.retrofit2:retrofit:${project.ext.retrofitVersion}"
    implementation "com.squareup.retrofit2:converter-gson:${project.ext.retrofitVersion}"

    debugImplementation project(':devbricks')
    debugAnnotationProcessor project(':devbricks-processors')
    releaseImplementation project(':devbricks')
    releaseAnnotationProcessor project(':devbricks-processors')
    releaseUnsignedImplementation project(':devbricks')
    releaseUnsignedAnnotationProcessor project(':devbricks-processors')
}
